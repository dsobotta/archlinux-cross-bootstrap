# $Id$
# Maintainer (Arch): Anatol Pomozov <anatol.pomozov@gmail.com>

_target=@CHOST@
pkgname=$_target-binutils
pkgver=2.30
pkgrel=1
pkgdesc='A set of programs to assemble and manipulate binary and object files for the @CARCH@ target'
arch=(x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.bz2{,.sig})
sha1sums=('33d807f7fa680b00439eb5560acd0c2ef645e5f9'
          'SKIP')
validpgpkeys=('EAF1C276A747E9ED86210CBAC3126D3B4AE55E93'  # Tristan Gingold <gingold@adacore.com>
              '3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F') # Nick Clifton (Chief Binutils Maintainer) <nickc@redhat.com>

prepare() {
  cd binutils-$pkgver
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd binutils-$pkgver

  ./configure --target=$_target \
              --prefix=/usr \
              --with-lib-path=/usr/lib:/usr/local/lib \
              --with-sysroot=/usr/$_target \
              --disable-werror \
              --disable-nls \
              --with-expat=yes \

  make
}

package() {
  cd binutils-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,nlmconv,windres,windmc}*
  # Remove info documents that conflict with host version
  rm -r "$pkgdir"/usr/share/info
}
